apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'com.td.HttpServer.Main'

repositories {
  mavenCentral()
}

run {
  if (project.hasProperty('args')) {
    args project.args.split('\\s+')
  }
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

jar {
  manifest {
    attributes(
      'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
      'Main-Class': 'com.td.HttpServer.Main'
      )
  }
  baseName = 'HttpServer'
  version = '0.1.6'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
  testCompile "junit:junit:4.11"
  integrationTestCompile "junit:junit:4.11"
}

test {
  testLogging {
    events 'passed'
  }
}

task integrationTest(type: Test) {
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

integrationTest {
  testLogging {
    events 'passed'
  }
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}
